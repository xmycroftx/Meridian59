// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
DeathSpider is Monster

constants:

   include blakston.khd

resources:

   include dethspid.lkod

   DeathSpider_koc_name_rsc = "na'arkinich"
   DeathSpider_name_rsc = "black spider"
   DeathSpider_icon_rsc = spidmagc.bgf
   DeathSpider_desc_rsc = \
      "Once thought to be a creature made up to frighten children, the "
      "black spider is said to be a product of evil sorcery, an unwholesome "
      "melding of human and insect.  Its origin, however, is different "
      "depending on the story.  In some, it is the fate of some poor soul "
      "who crossed a dark mage.  In others, the dark spider is an evil mage "
      "transformed in return for longevity.  All the old tales, however, "
      "warn travellers to beware of this malignant creature's extraordinarily "
      "poisonous bite."

   DeathSpider_dead_icon_rsc = spidmagX.bgf
   DeathSpider_dead_name_rsc = "dead spider"

   DeathSpider_sound_hit = spd_atkh.ogg
   DeathSpider_sound_miss = spd_atkm.ogg
   DeathSpider_sound_death = spd_dth.ogg
   DeathSpider_sound_aware = spd_awr.ogg

classvars:
   vrKocName = DeathSpider_koc_name_rsc
   vrName = DeathSpider_name_rsc
   vrIcon = DeathSpider_icon_rsc
   vrDesc = DeathSpider_desc_rsc
   vrDead_icon = DeathSpider_dead_icon_rsc
   vrDead_name = DeathSpider_dead_name_rsc

   // This monster poisons enemies...
   viPoisonChance = 10
   viPoisonStrength = 1000
   viPoisonDuration = 180000
   viPoisonHealth = 50
   viPoisonVigor = 10

   viTreasure_type = TID_BLACK_SPIDER
   viSpeed = SPEED_AVERAGE
   viAttack_type =  ATK_TYPE_BITE
   viAttributes = MOB_SPASM
   viLevel = 75
   viDifficulty = 5
   viVisionDistance = 12
   viKarma = -30
   viDefault_behavior = AI_FIGHT_AGGRESSIVE
   viWimpy = 30
   viCashmin = 375
   viCashmax = 950
   vrSound_hit = DeathSpider_sound_hit
   vrSound_miss = DeathSpider_sound_miss
   vrSound_aware = DeathSpider_sound_aware
   vrSound_death = DeathSpider_sound_death

   vbCanSiphon = FALSE

   // Quite used to dark places.
   viNightVision = 50

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [ -SPL_TYPE_ALL, 75 ],
                        [ ATK_TYPE_MAGIC, 75 ],
                        [ ATK_TYPE_NONMAGIC, -15 ] ];

      propagate;
   }

   DoSpasm()
   {
      Send(self,@DoLegSpasm);

      propagate;
   }

   DoLegSpasm()
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,150,2,2,2,7);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,200,2,1,2,1);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,8,2,12,2,1);

         return;
      }

      propagate;
   }

   IsAlliedClass(what=$)
   {
      if IsClass(what,&SpiderBaby)
         OR IsClass(what,&Spider)
         OR IsClass(what,&SpiderQueen)
      {
         return TRUE;
      }

      propagate;
   }

   CanBeWebbed()
   {
      return FALSE;
   }

end
////////////////////////////////////////////////////////////////////////////////
