// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
SpiderQueen is Monster

constants:

   include blakston.khd

resources:

   include spdrquen.lkod

   spiderqueen_name_rsc = "queen spider"
   spiderqueen_koc_name_rsc = "kokinich"
   spiderqueen_icon_rsc = spdrqu.bgf
   spiderqueen_desc_rsc = \
      "The huge egg sac of the queen spider makes her slow and heavy, but "
      "dark intelligence shines in her many red eyes."

   spiderqueen_dead_icon_rsc = spdrquX.bgf
   spiderqueen_dead_name_rsc = "dead queen spider"

   spiderqueen_sound_hit = qn_atkh.ogg
   spiderqueen_sound_miss = qn_atkm.ogg
   spiderqueen_sound_death = qn_dth.ogg
   spiderqueen_sound_aware = qn_awr.ogg

classvars:

   vrKocName = spiderqueen_koc_name_rsc
   vrName = spiderqueen_name_rsc
   vrIcon = spiderqueen_icon_rsc
   vrDesc = spiderqueen_desc_rsc
   vrDead_icon = spiderqueen_dead_icon_rsc
   vrDead_name = spiderqueen_dead_name_rsc

   // This monster poisons enemies...
   viPoisonChance = 7
   viPoisonStrength = 1000
   viPoisonDuration = 30000
   viPoisonHealth = 25
   viPoisonVigor = 50

   viTreasure_type = TID_SPIDER_QUEEN
   viSpeed = SPEED_NONE
   viAttack_type = ATK_TYPE_BITE
   viAttributes = MOB_NOMOVE
   viLevel = 165
   viDifficulty = 9

   // Low vision since we want her to switch targets if possible.
   viVisionDistance = 6

   // Attack range of 256 (4 row/col units).
   viMeleeRange = 256

   viKarma = -90
   viDefault_behavior = AI_NOMOVE | AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT
   viGender = GENDER_FEMALE
   viCashmin = 0
   viCashmax = 4000
   vrSound_hit = spiderqueen_sound_hit
   vrSound_miss = spiderqueen_sound_miss
   vrSound_aware = spiderqueen_sound_aware
   vrSound_death = spiderqueen_sound_death

   vbCanSiphon = FALSE

   // Very, very used to dark places.
   viNightVision = 200

   viBonusXP = 100

properties:

   piAnimation = ANIM_NONE

messages:

   Constructed()
   {
      plResistances = [ [-SPL_TYPE_ALL, 85],
                        [ATK_TYPE_MAGIC, 85],
                        [ATK_TYPE_BLUDGEON, 85],
                        [-SPL_TYPE_ACID, -50],
                        [ATK_TYPE_THRUST, -20]
                      ];

      propagate;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,200, 2,1, 2,1);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,1, 2,3, 2,1);

         return;
      }

      propagate;
   }

   DestroyDisposable()
   {
      return;
   }

   HitSideEffect(what = $, who = $)
   {
      local oSpell;

      if Random(1,4) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_SPIDER_WEB);
         if who <> $
         {
            Send(oSpell,@CastSpell,#who=who,#lTargets=[what],#ispellpower=30,
                  #bItemCast=TRUE);
         }
         else
         {
            Send(oSpell,@CastSpell,#who=self,
                  #lTargets=[poTarget],#ispellpower=100);
         }
      }

      propagate;
   }

   IsAlliedClass(what=$)
   {
      if IsClass(what,&DeathSpider)
         OR IsClass(what,&SpiderBaby)
         OR IsClass(what,&Spider)
      {
         return TRUE;
      }

      propagate;
   }

   CanBeWebbed()
   {
      return FALSE;
   }

end
////////////////////////////////////////////////////////////////////////////////
