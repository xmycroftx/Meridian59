// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
DuskRat is Monster

constants:

   include blakston.khd

resources:

   include duskrat.lkod

   duskrat_koc_name_rsc = "teotnapyijoa"
   duskrat_name_rsc = "dusk rat"
   duskrat_icon_rsc = duskrat.bgf

   duskrat_desc_rsc = \
      "The air around the rat is dark and thick with evil.  The smell of "
      "death and disease linger on its fetid breath as it darts towards "
      "its target.  The origins of this abomination are unknown.  The only "
      "definite knowledge known is of the deadly poison it carries "
      "in its bite."

   duskrat_poisoned_add_rsc = \
      "As the poison travels to your head, you begin "
      "to feel a little light-headed.  The room spins around you."

   duskrat_dead_icon_rsc = ratX.bgf
   duskrat_dead_name_rsc = "dead dusk rat"

   duskrat_sound_hit = rat_atkh.ogg
   duskrat_sound_miss = rat_atkm.ogg
   duskrat_sound_death = rat_dth.ogg
   duskrat_sound_aware = rat_awr.ogg

classvars:

   vrKocName = duskrat_koc_name_rsc
   vrName = duskrat_name_rsc
   vrIcon = duskrat_icon_rsc
   vrDesc = duskrat_desc_rsc
   vrDead_icon = duskrat_dead_icon_rsc
   vrDead_name = duskrat_dead_name_rsc
   viDead_drawfx = DRAWFX_BLACK

   // This monster poisons enemies...
   viPoisonChance = 17
   viPoisonStrength = 1000
   viPoisonDuration = 30000
   viPoisonHealth = 100
   viPoisonVigor = 50

   viTreasure_type = TID_RAT

   viSpeed = SPEED_VERY_FAST
   viAttack_type = ATK_TYPE_CLAW
   viAttributes = 0
   viLevel = 90
   viDifficulty = 6
   // Attack range of 192, or 3 row/col.
   viMeleeRange = 192
   viVisionDistance = 11
   viKarma = -40
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT
   viLight_Penalty = 3
   viCashmin = 250
   viCashmax = 750

   vrSound_hit = duskrat_sound_hit
   vrSound_miss = duskrat_sound_miss
   vrSound_aware = duskrat_sound_aware
   vrSound_death = duskrat_sound_death

   viPoisonVisualEffect = EFFECT_WAVER
   viPoisonVisualEffectDuration = 10000
   vrPoisonVisualEffectMessage = duskrat_poisoned_add_rsc

   vbCanSiphon = FALSE

   // Very used to dark places.
   viNightVision = 150

properties:

   piAnimation = ANIM_NONE
   piDrawfx = DRAWFX_BLACK

messages:

   Constructed()
   {
      plResistances = [ [-SPL_TYPE_FIRE, -30 ],
                        [ATK_TYPE_BLUDGEON, 10 ],
                        [ATK_TYPE_SLASH, 20 ]
                      ];

      propagate;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,125,2,2,2,6);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,75,2,2,2,6);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,100,2,7,2,11,2,1);

         return;
      }

      propagate;
   }

   NewOwner(what=$)
   "Make room darker when present, about 2X darker than a ShadowBeast"
   {
      //New room
      if what <> $
      {
         Send(what,@AddBaseLight,#amount=-viLight_Penalty);
      }

      //Old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

   Killed()
   {
      // old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

   Delete()
   {
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
