// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
GroundWorm is Monster

constants:

   include blakston.khd

   ANIM_POPUP = 2
   ANIM_POPDOWN = 3
   ANIM_POPUPATTACK = 4

   FRAME_UNDERGROUND = 1
   FRAME_POPUP_START = 2
   FRAME_POPUP_END = 6
   FRAME_SURFACED = 7
   FRAME_ATTACK_START = 8
   FRAME_ATTACK_END = 11
   FRAME_POPDOWN_START = 12
   FRAME_POPDOWN_END = 14

   ANIMATION_SPEED = 80

   POSTPOP_DELAY = 500     // half a second

resources:

   include grdworm.lkod

   GroundWorm_name_rsc = "groundworm"
   GroundWorm_icon_rsc = wormm.bgf
   GroundWorm_koc_name_rsc = "slithic"
   GroundWorm_desc_rsc = \
      "A fearsome and stealthy creature, the groundworm is known for its "
      "habit of lurking underground out of sight, then suddenly springing "
      "forth to devour its hapless victim."

   GroundWorm_dead_icon_rsc = wormmX.bgf
   GroundWorm_dead_name_rsc = "slain groundworm"

   GroundWorm_sound_miss = gwrmatk.ogg
   GroundWorm_sound_death = gwrmdie.ogg
   GroundWorm_sound_aware = gwrm1awr.ogg

   groundworm_cannot_hit = \
      "You are unable to reach the groundworm with your attacks."

classvars:

   vrKocName = GroundWorm_koc_name_rsc
   vrName = GroundWorm_name_rsc
   vrIcon = GroundWorm_icon_rsc
   vrDesc = GroundWorm_desc_rsc
   vrDead_icon = GroundWorm_dead_icon_rsc
   vrDead_name = GroundWorm_dead_name_rsc

   viTreasure_type = TID_TOUGH
   viSpeed = SPEED_FAST
   viSpell_type = SPL_TYPE_ACID
   viAttributes = 0
   viLevel = 100
   viDifficulty = 5
   viVisionDistance = 12
   viKarma = -30
   viDefault_behavior = AI_FIGHT_AGGRESSIVE
   viCashmin = -500
   viCashmax = 700
   vrSound_miss = GroundWorm_sound_miss
   vrSound_aware = GroundWorm_sound_aware
   vrSound_death = GroundWorm_sound_death

   viTrailLength = 3
   vbTrailLarge = TRUE

properties:

   piAnimation = ANIM_NONE
   plTrail = $
   piLastRow = 0
   piLastCol = 0

   pbSurfaced = FALSE

   ptPostPop = $

messages:

   Constructed()
   {
      plResistances = [ [ATK_TYPE_MAGIC, -20 ],
                        [ATK_TYPE_BLUDGEON, -30 ],
                        [-SPL_TYPE_ACID, 70 ],
                        [-SPL_TYPE_QUAKE, 50 ]
                      ];

      plActions = [ [SID_SPLASH_OF_ACID] ];

      propagate;
   }

   DoMeleeAnimation(what = $)
   {
      if pbSurfaced
      {
         piAnimation = ANIM_ATTACK;
         Send(poOwner,@SomethingChanged,#what=self);
         piAnimation = ANIM_NONE;
      }
      else
      {
         if ptPostPop = $
         {
            ptPostPop = createTimer(self,@PostPop,POSTPOP_DELAY);
            piAnimation = ANIM_POPUPATTACK;
            pbSurfaced = TRUE;
            Send(poOwner,@SomethingChanged,#what=self);
            piAnimation = ANIM_NONE;
         }
      }

      return;
   }

   PopDown()
   {
      if ptPostPop = $
      {
         ptPostPop = createTimer(self,@PostPop,POSTPOP_DELAY);
         piAnimation = ANIM_POPDOWN;
         pbSurfaced = FALSE;
         Send(poOwner,@SomethingChanged,#what=self);
         piAnimation = ANIM_NONE;
      }

      return;
   }

   PostPop()
   {
      ptPostPop = $;

      return;
   }

   EnterStateLimbo()
   {
      if pbSurfaced
      {
         Send(self,@PopDown);
      }

      propagate;
   }

   EnterStateWait()
   {
      if pbSurfaced
      {
         Send(self,@PopDown);
      }

      propagate;
   }

   EnterStateMove()
   {
      if pbSurfaced
      {
         Send(self,@PopDown);
      }

      propagate;
   }

   SendMoveAnimation()
   {
      Send(self,@SendAnimation);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         Send(self,@SendAttackAnimation);

         return;
      }

      if piAnimation = ANIM_POPUP
      {
         Send(self,@SendPopupAnimation);

         return;
      }

      if piAnimation = ANIM_POPDOWN
      {
         Send(self,@SendPopdownAnimation);

         return;
      }

      if piAnimation = ANIM_POPUPATTACK
      {
         Send(self,@SendPopupAttackAnimation);

         return;
      }

      if pbSurfaced
      {
         AddPacket(1,ANIMATE_NONE,2,FRAME_SURFACED);
      }
      else
      {
         AddPacket(1,ANIMATE_NONE,2,FRAME_UNDERGROUND);
      }

      return;
   }

   SendAttackAnimation()
   {
      AddPacket(1,ANIMATE_ONCE,4,ANIMATION_SPEED,2,FRAME_ATTACK_START,
            2,FRAME_ATTACK_END,2,FRAME_SURFACED);

      return;
   }

   SendPopupAnimation()
   {
      AddPacket(1,ANIMATE_ONCE,4,ANIMATION_SPEED,2,FRAME_POPUP_START,
            2,FRAME_POPUP_END,2,FRAME_SURFACED);

      return;
   }

   SendPopdownAnimation()
   {
      AddPacket(1,ANIMATE_ONCE,4,ANIMATION_SPEED,2,FRAME_POPDOWN_START,
            2,FRAME_POPDOWN_END,2,FRAME_UNDERGROUND);

      return;
   }

   SendPopupAttackAnimation()
   {
      AddPacket(1,ANIMATE_ONCE,4,ANIMATION_SPEED,2,FRAME_POPUP_START,
            2,FRAME_ATTACK_END,2,FRAME_SURFACED);

      return;
   }

   SendLookAnimation()
   {
      if pbSurfaced
      {
         AddPacket(1,ANIMATE_NONE,2,FRAME_SURFACED);
      }
      else
      {
         AddPacket(1,ANIMATE_NONE,2,FRAME_POPUP_START);
      }

      return;
   }

   NewOwner(what = $)
   {
      if Send(self,@GetOwner) <> what
      {
         // gotta post this so we look at our coords after we're in the new room
         Post(self,@InitializeTrail);
      }

      propagate;
   }

   InitializeTrail()
   {
      local i;

      if poOwner <> $
      {
         piLastRow = piRow;
         piLastCol = piCol;
      }

      foreach i in plTrail
      {
         Send(i,@Delete);
      }

      plTrail = $;

      return;
   }

   SomethingMoved(what=$,new_row=$,new_col=$)
   {
      local i,bAddTrailClump,oTrailClump;

      if what = self
      {
         if (piRow = piLastRow) AND (piCol = piLastCol)
         {
            propagate;
         }

         piLastRow = piRow;
         piLastCol = piCol;
         bAddTrailClump = TRUE;

         foreach i in plTrail
         {
            if Send(i,@GetOwner) = $
            {
               plTrail = DelListElem(plTrail,i);

               continue;
            }
            if (Send(i,@GetRow) = new_row) AND (Send(i,@GetCol) = new_col)
            {
               Send(i,@Delete);
               plTrail = DelListElem(plTrail,i);

               continue;
            }
            if (Send(i,@GetRow) = piLastRow) AND (Send(i,@GetCol) = piLastCol)
            {
               if bAddTrailClump
               {
                  bAddTrailClump = FALSE;
               }
               else
               {
                  Debug("Got two trail elements at the same location! ",
                        " Deleting the extra one.");
                  Send(i,@Delete);
                  plTrail = DelListElem(plTrail,i);
               }
            }
         }

         if bAddTrailClump
         {
            oTrailClump = Create(&GroundWormTrailClump,#TrailLarge=vbTrailLarge);
            Send(poOwner,@NewHold,#what=oTrailClump,
                  #new_row=piLastRow,#new_col=piLastCol);
            plTrail = Cons(oTrailClump,plTrail);
         }

         while Length(plTrail) > viTrailLength
         {
            oTrailClump = Nth(plTrail,Length(plTrail));
            Send(oTrailClump,@Delete);
            plTrail = DelListElem(plTrail,oTrailClump);
         }
      }

      propagate;
   }

   CanMonsterFight(who=$, oStroke = $, use_weapon = $)
   {
      if (oStroke <> $)
         AND (IsClass(oStroke,&DMSpell) OR IsClass(oStroke,&Smite))
      {
         return TRUE;
      }

      if NOT pbSurfaced
      {
         if use_weapon = $
            OR NOT (IsClass(use_weapon,&Hammer)
               OR IsClass(use_weapon,&SpiritualHammer)
               OR IsClass(use_weapon,&Mace))
         {
            Send(who,@MsgSendUser,#message_rsc=groundworm_cannot_hit);

            return FALSE;
         }
      }

      return TRUE;
   }

   Killed(what = $)
   {
      // Delete this now, in case this timer is active.
      if ptPostPop <> $
      {
         DeleteTimer(ptPostPop);
      }

      ptPostPop = $;

      propagate;
   }

   Delete()
   {
      local i;

      foreach i in plTrail
      {
         Send(i,@Delete);
      }
      plTrail = $;

      if ptPostPop <> $
      {
         DeleteTimer(ptPostPop);
      }
      ptPostPop = $;

      propagate;
   }

   CanMorphTo()
   {
      return FALSE;
   }

   ////  Action Routines
   EngageTimer( timer = $ )
   "We're trying to engage our target, so either we keep"
   "chasing, or we attack. Note that even if we can't move,"
   "we can still be 'engaging'...."
   {
      local i,oVictim, iDistance, iChaseRange, iDelay, bSight;
      oVictim = poTarget;

      Send(self,@ClearBehavior,#timer=timer);

      // Sanity checks
      if poOwner = $
      {
         Debug("Unreachable. Null owner.");
         Send(self,@EnterStateLimbo);

         return;
      }

      // If target left, we should already know
      if oVictim = $
      {
         //Debug("Unreachable. Null victim.");
         Send(self,@EnterStateWait);

         return;
      }

      // Victim phased out or is now in spectator mode, break aggro.
      if IsClass(oVictim,&User)
         AND Send(oVictim,@IsInCannotInteractMode)
      {
         Send(self,@EnterStateMove);

         return;
      }

      // Get distance to target and check LoS.
      iDistance = Send(self,@SquaredFineDistanceTo3D,#what=oVictim);
      bSight = Send(poOwner,@LineOfSight,#obj1=self,#obj2=oVictim);

      // Mobs may stop chasing if the target gets out of range.
      // Not if it has hurt us recently though.
      if NOT Send(self,@GetHurtMeRecentlyAmount,#who=oVictim)
      {
         iChaseRange =  piVisionDistance * FINENESS;
         iChaseRange = iChaseRange * (50 + piStamina) / 50;
         if NOT bSight
         {
            iChaseRange /= 5;
         }
         iChaseRange *= iChaseRange;

         // Mobs may lose interest if the target is too far away or out of sight.
         if (NOT ((piBehavior & AI_FIGHT_SINGLEMINDED) OR (poMaster <> $)))
            AND (iDistance > iChaseRange)
         {
            Send(self,@EnterStateMove);

            return;
         }
      }

      if Send(self,@MonsterCombatCheck)
      {
         iDelay = Send(self,@MonsterCombatAction,#distance=iDistance,#sight=bSight);
      }
      else
      {
         iDelay = 0;
      }

      if (iDelay = 0) AND pbSurfaced
      {
         Send(self,@PopDown);
      }

      // We may no longer be around after the attack (e.g. evil twin killing a target).
      if poOwner = $
      {
         return;
      }

      // Got gassed after acting and is currently resting. Quit.
      if (piState & STATE_REST)
      {
         return;
      }

      // If we can move, we physically try to get closer.
      if iDelay = 0
      {
         Send(poBrain,@GetEngageBehavior,#mob=self,#target=poTarget,#behavior=piBehavior);
      }

      // Monster acted. Pass on the appriopriate delay.
      if poTarget <> $
      {
         Send(poBrain,@PostEngageTimer,#mob=self,#delay=Abs(iDelay));
      }
      else
      {
         // Target died. Move on.
         Send(self,@EnterStateMove);
      }

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
