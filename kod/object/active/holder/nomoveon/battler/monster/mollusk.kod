// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
MolluskMonster is Monster

// TODO: Make the creature hide in his shell until he's aware of someone or
//       he's attacked.

constants:

   include blakston.khd

   ANIM_POPUP = 2
   ANIM_THROW = 3

   FRAME_STANDING = 1
   FRAME_WALK_START = 2
   FRAME_WALK_END = 6
   FRAME_ATTACK_START = 7
   FRAME_ATTACK_END = 10
   FRAME_THROW_START = 11
   FRAME_THROW_END = 14
   FRAME_POPUP_START = 15
   FRAME_POPUP_END = 19

   ANIMATION_WALK_SPEED = 80
   ANIMATION_ATTACK_SPEED = 225
   ANIMATION_THROW_SPEED = 80
   ANIMATION_POPUP_SPEED = 200

   // Time from creation to "popping up"
   POPUP_DELAY = 2500

resources:

   include mollusk.lkod

   MolluskMonster_koc_name_rsc = "kolisith"
   MolluskMonster_name_rsc = "mollusk creature"
   MolluskMonster_icon_rsc = mollusk.bgf
   MolluskMonster_desc_rsc = \
      "A recent arrival to the lands, this giant creature is rather "
      "peaceful.  It roams the area, looking for food.  It rarely attacks "
      "people unless it is attacked first.  But, be warned, its thick shell "
      "and powerful claw make it a tough foe to face."

   MolluskMonster_dead_icon_rsc = molX.bgf
   MolluskMonster_dead_name_rsc = "slain mollusk creature"

   MolluskMonster_sound_miss = mol_atk.ogg
   MolluskMonster_sound_death = mol_dth.ogg
   MolluskMonster_sound_aware = mol_awr.ogg

   MolluskMonster_rock_small = rocksm.bgf
   MolluskMonster_rock_large = rockla.bgf

classvars:

   vrKocName = MolluskMonster_koc_name_rsc
   vrName = MolluskMonster_name_rsc
   vrIcon = MolluskMonster_icon_rsc
   vrDesc = MolluskMonster_desc_rsc
   vrDead_icon = MolluskMonster_dead_icon_rsc
   vrDead_name = MolluskMonster_dead_name_rsc

   viTreasure_type = TID_MOLLUSK
   viSpeed = SPEED_FAST
   viDefault_behavior = AI_MOVE_OPTIMAL_RANGE | AI_FIGHT_NEWBIESAFE
   viAttack_type = ATK_TYPE_BLUDGEON

   viAttributes = 0
   viLevel = 150
   viDifficulty = 8
   viVisionDistance = 20
   // Attack range of 192, or 3 row/col.
   viMeleeRange = 192
   viFireRange = 512
   viKarma = 100
   viCashmin = 400
   viCashmax = 1300
   vrSound_miss = MolluskMonster_sound_miss
   vrSound_aware = MolluskMonster_sound_aware
   vrSound_death = MolluskMonster_sound_death

   // Mollusks very rarely team up.
   viSocial = 2

   vbCanSiphon = FALSE

   vbAquatic = TRUE

properties:

   piAnimation = ANIM_NONE
   ptActive = $

   // piSpellRange in FINENESS units. 10 row/cols.
   //piSpellRange = 640

messages:

   Constructed()
   {
      plResistances = [ [ATK_TYPE_MAGIC, -5 ],
                        [ATK_TYPE_ALL, 15 ],
                        [-SPL_TYPE_QUAKE, 50 ]
                      ];

      piAnimation = ANIM_NONE;
      if poOwner <> $
      {
         ptActive = CreateTimer(self,@PopupTimer,POPUP_DELAY);
         Send(poOwner,@SomethingChanged,#what=self);
      }

      propagate;
   }

   Delete()
   {
      if ptActive <> $
      {
         DeleteTimer(ptActive);
         ptActive = $;
      }

      propagate;
   }

   PopupTimer()
   {
      ptActive = $;
      piAnimation = ANIM_POPUP;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_NONE
      {
         if ptActive = $
         {
            AddPacket(1,ANIMATE_NONE, 2,FRAME_STANDING);
         }
         else
         {
            AddPacket(1,ANIMATE_NONE, 2,FRAME_POPUP_START);
         }

         return;
      }

      if piAnimation = ANIM_ATTACK
      {
         Send(self,@SendAttackAnimation);

         return;
      }

      if piAnimation = ANIM_THROW
      {
         Send(self,@SendThrowAnimation);

         return;
      }

      if piAnimation = ANIM_POPUP
      {
         Send(self,@SendPopupAnimation);

         return;
      }

      propagate;
   }

   SendAttackAnimation()
   {
      AddPacket(1,ANIMATE_ONCE, 4,ANIMATION_ATTACK_SPEED,
                2,FRAME_ATTACK_START, 2,FRAME_ATTACK_END, 2,FRAME_STANDING);

      return;
   }

   SendThrowAnimation()
   {
      AddPacket(1,ANIMATE_ONCE, 4,ANIMATION_THROW_SPEED,
                2,FRAME_THROW_START, 2,FRAME_THROW_END, 2,FRAME_STANDING);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,ANIMATION_WALK_SPEED,
                2,FRAME_WALK_START, 2,FRAME_WALK_END);

      return;
   }

   SendPopupAnimation()
   {
      AddPacket(1,ANIMATE_ONCE, 4,ANIMATION_POPUP_SPEED,
                2,FRAME_POPUP_START, 2,FRAME_POPUP_END, 2,FRAME_STANDING);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_NONE, 2,FRAME_STANDING);

      return;
   }

   DoMissileAnimation()
   {
      local oElement;

      ptAction = $;

      if (poOwner <> $) AND (poTarget <> $)
      {
         piAnimation = ANIM_THROW;
         Send(poOwner,@SomethingChanged,#what=self);
         piAnimation = ANIM_NONE;
         Send(poOwner,@SomethingShot,#who=self,#target=poTarget,#projectile=self);

         if Random(1,10) < 2
         {
            oElement = Create(&FogCloud,#seed=[self,99,5000,0,0,0,0,0,0]);
         }
         else if Random(1,10) = 10
         {
            oElement = Create(&PoisonFogCloud,#seed=[self,99,5000,0,0,0,0,0,0]);
         }
         else
         {
            oElement = Create(&ActiveSporeCloud,#seed=[self,99,5000,0,0,0,0,0,0]);
         }

         Send(poOwner,@NewHold,#what=oElement,
            #new_row=Send(poTarget,@GetRow),#new_col=Send(poTarget,@GetCol),
            #fine_row=Random(1,64),#fine_col=Random(1,64));

         --piAction;

         if piAction > 0
         {
            ptAction = CreateTimer(self,@DoMissileAnimation,400);
         }
      }

      return;
   }

   GetProjectileSpeed()
   {
      return 8;
   }

   SendProjectileAnimation()
   {
      // Both rocks cycle from frame 1 to 4.
      AddPacket(1,ANIMATE_CYCLE, 4,175, 2,1, 2,4);  

      return;
   }

   GetProjectileIcon()
   {
      if Random(1,100) < 30
      {
         return MolluskMonster_rock_large;
      }

      return MolluskMonster_rock_small;
   }

   GetProjectileLightFlags()
   {
      // No lighting information.
      return LIGHT_FLAG_NONE;
   }

end
////////////////////////////////////////////////////////////////////////////////
