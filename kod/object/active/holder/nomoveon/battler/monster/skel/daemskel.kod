// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
DaemonSkeleton is Skeleton

constants:

   include blakston.khd

   DEMENTIA_CHANCE = 20

resources:

   include daemskel.lkod

   DaemonSkeleton_koc_name_rsc = "kochaleoc"
   DaemonSkeleton_name_rsc = "daemon skeleton"
   DaemonSkeleton_icon_rsc = skelbody.bgf
   DaemonSkeleton_desc_rsc = \
      "The sight alone of this abomination of nature is often enough to "
      "paralyze a human.  You shudder to think what dark ritual brought "
      "this creature into being."

   DaemonSkeleton_head = skelhed4.bgf
   DaemonSkeleton_dead_icon_rsc = skelX.bgf
   DaemonSkeleton_dead_name_rsc = "pile of bones"

   DaemonSkeleton_sound_miss = skl4_atk.ogg
   DaemonSkeleton_sound_death = skl4_die.ogg
   DaemonSkeleton_sound_aware = skl4_awr.ogg

classvars:

   vrKocName = DaemonSkeleton_koc_name_rsc
   vrName = DaemonSkeleton_name_rsc
   vrIcon = DaemonSkeleton_icon_rsc
   vrDesc = DaemonSkeleton_desc_rsc
   vrDead_icon = DaemonSkeleton_dead_icon_rsc
   vrDead_name = DaemonSkeleton_dead_name_rsc

   viTreasure_type = TID_SKELETON4
   viSpeed = SPEED_FAST
   viAttack_type = ATK_TYPE_THRUST
   viAttributes = 0
   viLevel = 130
   viDifficulty = 8
   viVisionDistance = 15
   // Attack range of 192, or 3 row/col.
   viMeleeRange = 192
   viKarma = -90
   viDefault_behavior = \
      AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT | AI_MOVE_FLEE_FRIGHTENERS
   viCashmin = 350
   viCashmax = 1025
   vrSound_miss = DaemonSkeleton_sound_miss
   vrSound_aware = DaemonSkeleton_sound_aware
   vrSound_death = DaemonSkeleton_sound_death

   vrHeadIcon = DaemonSkeleton_head

   viBleedChance = 10
   viBleedStrength = 2000
   viBleedDuration = 10000

properties:

   piAnimation = ANIM_NONE

messages:

   SetResistances()
   {
      plResistances = [ [ATK_TYPE_PIERCE, 70 ],
                        [ATK_TYPE_THRUST, 70 ],
                        [-SPL_TYPE_UNHOLY, 70 ],
                        [-SPL_TYPE_SHOCK, 70 ],
                        [-SPL_TYPE_FIRE, 70 ],
                        [-SPL_TYPE_COLD, -10 ],
                        [-SPL_TYPE_HOLY, -20 ],
                        [ATK_TYPE_BLUDGEON, -20 ]
                      ];

      return;
   }

   HitSideEffect(what = $, who = $)
   {
      local oSpell;

      oSpell = Send(SYS,@FindSpellByNum,#num=SID_DEMENT);
      if NOT Send(what,@IsEnchanted,#what=oSpell)
         AND Random(1,DEMENTIA_CHANCE) = 1
      {
         if who <> $
         {
            Send(oSpell,@CastSpell,#who=who,#lTargets=[what],#iSpellPower=15,
                  #bItemCast=TRUE);
         }
         else
         {
            Send(oSpell,@DoSpellEffects,#who=self,#lTargets=[what],
                  #iSpellPower=15);
         }
      }

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
