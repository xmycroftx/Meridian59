// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
OldOrc is Monster

constants:

   include blakston.khd

resources:

   include oldorc.lkod

   oldorc_koc_name_rsc = "ikutomca"
   oldorc_name_rsc = "orc slave"
   oldorc_icon_rsc = oldorc.bgf
   oldorc_desc_rsc = \
      "This misshapen orc carries no weapon, but has the strength to beat "
      "an adventurer to death with its bare hands."

   oldorc_dead_icon_rsc = oldorcX.bgf
   oldorc_dead_name_rsc = "dead orc slave"

   oldorc_sound_hit = orc_atkh.ogg
   oldorc_sound_miss = orc_atkm.ogg
   oldorc_sound_death = orc_dth.ogg
   oldorc_sound_aware = orc_awr.ogg

classvars:

   viIndefinite = ARTICLE_AN
   vrKocName = oldorc_koc_name_rsc
   vrName = oldorc_name_rsc
   vrIcon = oldorc_icon_rsc
   vrDesc = oldorc_desc_rsc
   vrDead_icon = oldorc_dead_icon_rsc
   vrDead_name = oldorc_dead_name_rsc

   viTreasure_type = TID_ORC

   viSpeed = SPEED_AVERAGE
   viAttack_type = ATK_TYPE_BLUDGEON
   viAttributes = 0
   viLevel = 50
   viDifficulty = 6
   viVisionDistance = 10
   viKarma = -40
   viDefault_behavior = \
      AI_FIGHT_AGGRESSIVE | AI_MOVE_FLEE_FRIGHTENERS | AI_FIGHT_SWITCHALOT
   viCashmin = 150
   viCashmax = 450

   viWimpy = 10

   vrSound_hit = oldorc_sound_hit
   vrSound_miss = oldorc_sound_miss
   vrSound_aware = oldorc_sound_aware
   vrSound_death = oldorc_sound_death

   viBleedChance = 15
   viBleedStrength = 1000
   viBleedDuration = 5000

   vbCanSiphon = FALSE

   // Quite used to dark places.
   viNightVision = 100

properties:

   piAnimation = ANIM_NONE
   piReputationGroup = REP_ORC
   plReputationEnemies = $

messages:

   Constructed()
   {
      plResistances = [ [-SPL_TYPE_UNHOLY, 40 ],
                        [ATK_TYPE_PIERCE, 40 ],
                        [ATK_TYPE_SLASH, 20 ],
                        [-SPL_TYPE_HOLY, -25 ] ];

      plReputationEnemies = [REP_TROLL];

      propagate;
   }

   // Used for defense messages
   GetParryAbility(stroke_obj=$)
   {
      if stroke_obj <> $
         AND NOT Send(stroke_obj,@CanParry)
      {
         return 0;
      }

      return (viDifficulty*10)/2;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,200, 2,2, 2,5);

      return;
   }

   SendMoveAnimation()
   {
      // Make it look like orc is walking
      // Cycle through "walking" bitmaps (groups 2-5) at 50 ms per frame
      AddPacket(1,ANIMATE_CYCLE, 4,200, 2,2, 2,5);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,7, 2,10, 2,1);

         return;
      }

      /// if no body animation.
      propagate;
   }

   IsFrightenedOf(target=$)
   {
      return IsClass(target,&GiantRat);
   }

   IsAlliedClass(what=$)
   {
      if IsClass(what,&Orc)
         OR IsClass(what,&CaveOrc)
         OR IsClass(what,&OrcWizard)
         OR IsClass(what,&OrcPitBoss)
      {
         return TRUE;
      }

      propagate;
   }
   
   IsArenaMonster()
   {
      return FALSE;
   }

end
////////////////////////////////////////////////////////////////////////////////
