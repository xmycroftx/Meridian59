// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
WoodenShield is Shield

constants:

   include blakston.khd

resources:

   include wdshld.lkod

   woodenshield_name_rsc = "wooden shield"
   WoodenShield_icon_rsc = wdshield.bgf
   woodenshield_window_overlay_rsc = povshldF.bgf
   woodenshield_desc_rsc = \
      "A shield made of tough hard wood, it offers basic protection.  "
      "However, due to it being made of wood, it easily burns to a crisp if "
      "set on fire."
   WoodenShield_burn = \
      "Your wooden shield begins to catch fire and you quickly put it out."
   WoodenShield_burn_break = "~IYour wooden shield burned to a crisp!"

classvars:

   vrName = woodenshield_name_rsc
   vrIcon = woodenshield_icon_rsc
   vrDesc = woodenshield_desc_rsc

   viHits_init_min = 250
   viHits_init_max = 300

   viValue_average = 160
   viWeight = 100
   viBulk = 125

   vrShield_window_overlay = woodenshield_window_overlay_rsc
   vrShield_overlay = woodenshield_icon_rsc

   viGround_group = 3
   viInventory_group = 1

   viDefense_base = 5
   viDamage_base = 1

properties:

messages:

   GetResistanceModifiers()
   {
      return [ [ATK_TYPE_SLASH,10]
             ];
   }

   GetHeatDamage()
   {
      return 0;
   }

   ModifyDefenseDamage(who = $,what = $,damage = $,atype = 0,stype = 0)
   "Fire attacks will do damage directly to the shield."
   {
      if stype & SPL_TYPE_FIRE
      {
         Post(self,@BurnShield,#damage=damage);
      }
   
      propagate;
   }

   BurnShield(damage = $)
   {
      // Convert to low-resolution damage.
      damage /= 100;

      piHits -= Random(damage / 2, damage);

      if piHits < 5
      {
         if IsClass(poOwner,&User)
         {
            Send(poOwner,@MsgSendUser,#message_rsc=WoodenShield_burn_break);
         }

         Post(self,@Delete);
      }
      else
      {
         Send(poOwner,@MsgSendUser,#message_rsc=WoodenShield_burn);
      }

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
