// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
WeapAttBleed is WeaponAttribute

///////////////////////////////////////////////////////////////////////
//
//   This weapon will drain a target's health over time.
//
//   Form is:
//
//      [ WA_BLEED ]
//
///////////////////////////////////////////////////////////////////////

constants:

   include blakston.khd

resources:

   include wableed.lkod

   WeapAttBleed_desc = \
      "  The edges of this weapon are jagged and stained with blood."
   Bleed_dm = "bleed"
   Bleed_worked_target = \
      "%s%s bleeds profusely from cuts inflicted by your weapon!"
   Bleed_info_desc = "%i%% chance to bleed target on hit\n"

classvars:

   vrDesc = WeapAttBleed_desc
   vrInfoDesc = Bleed_info_desc
   viItem_Att_Num = WA_BLEED
   vrDM_trigger = Bleed_dm
   vbProcRequirement = TRUE

properties:

   // Modify the object's price by percent
   piValue_modifier = 200

   piValue_power_modifier = 10

   // Percent chance the effect procs
   piEffect_percent = 15

   viBleedStrength = 2000
   viBleedDuration = 3000

messages:

   // Effect Functions

   ModifyDamage(damage=0,target=$,wielder=$,lData=$)
   {
      local oEffect;

      if target = $
         OR wielder = $
      {
         return damage;
      }

      if Random(1,100) < piEffect_percent
         AND Send(target,@CanBleed)
      {
         oEffect = Send(SYS,@FindSpellByNum,#num=SID_BLEED);
         Send(oEffect,@StartBleed,#who=target,#source=wielder,
               #strength=viBleedStrength,#duration=viBleedDuration);

         Send(wielder,@MsgSendUser,#message_rsc=Bleed_worked_target,
               #parm1=Send(target,@GetCapDef),#parm2=Send(target,@GetName));
      }

      return Damage;
   }

   // Add the spell name to the description.
   AppendDescInfo(oItem = $,compound = $, bShowAll = FALSE)
   {
      if (bShowAll
         OR Send(oItem,@GetIDStatusFromCompound,#compound=compound))
      {
         AddPacket(4,vrInfoDesc, 4,piEffect_percent);
      }
      else
      {
         // Not identified, add nothing (blank resource).
         AddPacket(4,system_blank_resource);
      }

      return;
   }

   IsMagicalEffect()
   {
      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
