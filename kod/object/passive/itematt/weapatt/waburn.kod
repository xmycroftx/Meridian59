// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
WeapAttBurn is WeaponAttribute

///////////////////////////////////////////////////////////////////////
//
//   This weapon will deal a burn effect to target.
//
//   Form is:
//
//      [ WA_BURN ]
//
///////////////////////////////////////////////////////////////////////

constants:

   include blakston.khd

resources:

   include waburn.lkod

   WeapAttBurn_desc = \
      "  This weapon is hot to the touch, and small flames periodically dance "
      "across it."
   Burn_dm = "burn"
   Burn_worked_target = \
      "%s%s is set alight as your weapon strikes %s!"
   Burn_info_desc = \
      "%i%% chance to burn target on hit\n"

classvars:

   vrDesc = WeapAttBurn_desc
   vrInfoDesc = Burn_info_desc
   viItem_Att_Num = WA_BURN
   vrDM_trigger = Burn_dm
   vbProcRequirement = TRUE

properties:

   // Modify the object's price by percent
   piValue_modifier = 200

   piValue_power_modifier = 10

   // Percent chance the effect procs
   piEffect_percent = 15

   viBurnStrength = 2000
   viBurnDuration = 3000

messages:

   // Effect Functions

   ModifyDamage(damage=0,target=$,wielder=$,lData=$)
   {
      local oEffect;

      if target = $
         OR wielder = $
      {
         return damage;
      }

      if Random(1,100) < piEffect_percent
         AND Send(target,@CanBurn)
      {
         oEffect = Send(SYS,@FindSpellByNum,#num=SID_BURN);
         Send(oEffect,@StartBurn,#who=target,#source=wielder,
               #strength=viBurnStrength,#duration=viBurnDuration);

         Send(wielder,@MsgSendUser,#message_rsc=Burn_worked_target,
               #parm1=Send(target,@GetCapDef),#parm2=Send(target,@GetName),
               #parm3=Send(target,@GetHimHer));
      }

      return Damage;
   }

   // Add the spell name to the description.
   AppendDescInfo(oItem = $,compound = $, bShowAll = FALSE)
   {
      if (bShowAll
         OR Send(oItem,@GetIDStatusFromCompound,#compound=compound))
      {
         AddPacket(4,vrInfoDesc, 4,piEffect_percent);
      }
      else
      {
         // Not identified, add nothing (blank resource).
         AddPacket(4,system_blank_resource);
      }

      return;
   }

   IsMagicalEffect()
   {
      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
