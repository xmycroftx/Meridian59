// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
WeapAttSlow is WeaponAttribute

///////////////////////////////////////////////////////////////////////
//
//  This weapon has a chance to cast slow
//
//   Form is:
//
//      [ WA_SLOW]
//
///////////////////////////////////////////////////////////////////////

constants:

   include blakston.khd

resources:

   include waslow.lkod

   WeapAttSlow_desc = \
      "  The air around this weapon seems to be condensing and glowing with "
      "magical energy."
   slow_dm = "slow"
   slow_info_desc = "%i%% chance to cast Slow on hit\n"

classvars:

   vrDesc = WeapAttSlow_desc
   vrInfoDesc = slow_info_desc
   viItem_Att_Num = WA_SLOW
   viDifficulty = 10
   vrDM_trigger = slow_dm
   vbAppears_in_Happyland = FALSE
   vbAllowSpecialAttributes = FALSE
   vbProcRequirement = TRUE

properties:

   // Modify the object's price by percent
   piValue_modifier = 200

   piValue_power_modifier = 10
   
   // Percent chance the effect procs
   piEffect_percent = 15

   piSpellPower = 60

messages:

   // Effect Functions

   ModifyDamage(damage=0,target=$,wielder=$,lData=$)
   {
      local oSpell;

      if target = $
         OR wielder = $
      {
         return damage;
      }

      if Random(1,100) <= piEffect_percent
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_SLOW);

         /// no duplicates
         if NOT Send(target,@IsEnchanted,#what=oSpell)
         {
            Send(oSpell,@CastSpell,#who=wielder,#lTargets=[target],
                  #iSpellPower=piSpellPower,#bItemCast=TRUE);
         }
      }

      return damage;
   }

   // Add the spell name to the description.
   AppendDescInfo(oItem = $,compound = $, bShowAll = FALSE)
   {
      if (bShowAll
         OR Send(oItem,@GetIDStatusFromCompound,#compound=compound))
      {
         AddPacket(4,vrInfoDesc, 4,piEffect_percent);
      }
      else
      {
         // Not identified, add nothing (blank resource).
         AddPacket(4,system_blank_resource);
      }

      return;
   }

   IsMagicalEffect()
   {
      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
