// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
WeapAttBonker is WeaponAttribute

///////////////////////////////////////////////////////////////////////
//
//  This weapon just bonks the opponent.  It's nothing more than silly.
//
//  Form is:
//
//       [ WA_BONK ]
//	
///////////////////////////////////////////////////////////////////////

constants:

   include blakston.khd

resources:

   include wabonker.lkod

   weapattbonker_desc = \
      " Mocker's signature flamboyantly emblazons the pommel."
   bonker_dm = "bonker"
   bonker_info_desc = "%i%% chance to cast Bonk on hit\n"

classvars:

   vrDesc = weapattbonker_desc
   vrInfoDesc = bonker_info_desc
   viItem_Att_Num = WA_BONKER

   viDifficulty = 9
   vrDM_trigger = bonker_dm

properties:

   // Modify the object's price by percent
   piValue_modifier = 150

   piValue_power_modifier = 10

   // Percent chance the effect procs
   piEffect_Percent = 8

messages:

   // Effect Functions

   ModifyDamage(damage=0,target=$,wielder=$,lData=$)
   "Wielder has a 1% chance of blinding target with any hit."
   {
      local oSpell;

      if target = $
         OR wielder = $
      {
         return damage;
      }

      if Random(1,100) <= piEffect_percent
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_BONK);
         Send(oSpell,@CastSpell,#who=wielder,#lTargets=[target],
               #iSpellPower=50,#bItemCast=TRUE);
      }

      return Damage;
   }

   // Add the spell name to the description.
   AppendDescInfo(oItem = $,compound = $, bShowAll = FALSE)
   {
      if (bShowAll
         OR Send(oItem,@GetIDStatusFromCompound,#compound=compound))
      {
         AddPacket(4,vrInfoDesc, 4,piEffect_percent);
      }
      else
      {
         // Not identified, add nothing (blank resource).
         AddPacket(4,system_blank_resource);
      }

      return;
   }

   IsMagicalEffect()
   {
      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
