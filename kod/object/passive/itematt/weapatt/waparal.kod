// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
WeapAttParalyzer is WeaponAttribute

///////////////////////////////////////////////////////////////////////
//
//   This weapon has a chance to blind the opponent every time it does
//   damage.
//
//   Form is:
//
//      [ WA_Paralyzer]
//
///////////////////////////////////////////////////////////////////////

constants:

   include blakston.khd

resources:

   include waparal.lkod

   weapattParalyzer_desc = \
      " A glow that can only be described as Zjiriaesqe surrounds the weapon."
   Paralyzer_dm = "Paralyzer"
   holder_worked = "%s%s seems rooted to the very ground!"
   holder_info_desc = "%i%% chance to cast Hold on hit\n"

classvars:

   vrDesc = weapattParalyzer_desc
   vrInfoDesc = holder_info_desc
   viItem_Att_Num = WA_Paralyzer

   viDifficulty = 9
   vrDM_trigger = Paralyzer_dm
   vbAllowSpecialAttributes = FALSE
   vbProcRequirement = TRUE

properties:

   // Modify the object's price by percent
   piValue_modifier = 200

   piValue_power_modifier = 10
   
   // Percent chance the effect procs
   piEffect_percent = 15
   
   piSpellPower = 50

messages:

   // Effect Functions

   ModifyDamage(damage=0,target=$,wielder=$,lData=$)
   "Wielder has a chance of paralyzing target with any hit."
   {
      local oSpell;

      if target = $
         OR wielder = $
      {
         return damage;
      }

      if Random(1,100) <= piEffect_percent
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_HOLD);

         /// no duplicates
         if NOT Send(target,@IsEnchanted,#what=oSpell)
         {
            Send(oSpell,@CastSpell,#who=wielder,#lTargets=[target],
                  #iSpellPower=piSpellPower,#bItemCast=TRUE);
            Send(wielder,@MsgSendUser,#message_rsc=holder_worked,
                  #parm1=Send(target,@GetCapDef),#parm2=Send(target,@GetName));
         }
      }

      return damage;
   }

   // Add the spell name to the description.
   AppendDescInfo(oItem = $,compound = $, bShowAll = FALSE)
   {
      if (bShowAll
         OR Send(oItem,@GetIDStatusFromCompound,#compound=compound))
      {
         AddPacket(4,vrInfoDesc, 4,piEffect_percent);
      }
      else
      {
         // Not identified, add nothing (blank resource).
         AddPacket(4,system_blank_resource);
      }

      return;
   }

   IsMagicalEffect()
   {
      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////