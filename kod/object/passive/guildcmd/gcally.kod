// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
GuildForgeAlliance is GuildCommand

constants:

   include blakston.khd
   include protocol.khd

resources:

   include gcally.lkod

   guildforgealliance_name_rsc = "Forge Alliance"
   guildforgealliance_icon_rsc = light.bgf
   guildforgealliance_desc_text_rsc = \
      "Used to team up with other guilds.  Primary benefits of being in "
      "an alliance - sharing guild halls."

   ally_cant_ally_self = \
      "You want to forge an alliance with yourself?  Very well, you accept."
   ally_on_enemy_list = \
      "You may not become allies with %s%q until you cease "
      "hostilities with them."
   ally_on_ally_list = \
      "The fierce warriors of %s%q are already your allies!"

classvars:

   vrName = guildforgealliance_name_rsc
   vrIcon = guildforgealliance_icon_rsc
   vrDesc = guildforgealliance_desc_text_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viGuildcommand_num = GCID_FORGE_ALLIANCE
   viShow_enchantment_icon = ENCHANTMENT_SHOW_ALL
   viRank_needed = RANK_LIEUTENANT

properties:

messages:

   GetNumSpellTargets()
   {
      return 1;
   }

   DoCommand(who=$, otarget = $)
   "Create invitation and give it to the target."
   {
      local oGuild, ownguild, rank, i;

      oGuild = oTarget;
      ownguild = Send(who,@GetGuild);

      if oGuild = ownguild
      {
         Send(who,@MsgSendUser,#message_rsc=ally_cant_ally_self);

         return FALSE;
      }

      foreach i in Send(ownguild,@GetEnemyList)
      {
         if i = oGuild
         {
            Send(who,@MsgSendUser,#message_rsc=ally_on_enemy_list,
                  #parm1=Send(ownguild,@GetDef),#parm2=Send(ownguild,@GetName));

            return FALSE;
         }
      }

      foreach i in Send(ownGuild,@GetAllyList)
      {
         if i = oguild
         {
            Send(who,@MsgSendUser,#message_rsc=ally_on_ally_list,
                  #parm1=Send(oguild,@GetCapDef),#parm2=Send(oguild,@GetName));

            return FALSE;
         }
      }

      if Send(ownguild,@CanAlly,#otherguild=oGuild,#rep=who)
      {
         Send(ownguild,@NewAlly,#otherguild=oGuild);
      }

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
