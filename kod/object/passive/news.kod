// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
News is PassiveObject

constants:

   include blakston.khd

resources:

   include news.lkod

   news_name_rsc = "Newscroll"
   news_icon_rsc = news.bgf
   news_desc_rsc = "You are looking at a public posting device."

   news_users_cant_delete = \
      "Only DMs, Admins and Moderators may delete posts from this newsglobe."
   news_not_own_post = \
      "You can only delete your own posts."
   news_del_time_expired_header = \
      "You may only delete your post within %r%r%r%r%r%r%r of posting."
   news_new_post = \
      "~B~U~k[###]~n ~B~vNew post on %s (index %i) by ~r%s~n, title: ~b%q~n.  "
      "Post index %i."

classvars:

   vrIcon = news_icon_rsc

   // The number of minutes a user has to delete their post after posting it.
   viPostDeleteMinutes = 60

properties:

   vrName = news_name_rsc
   vrDesc = news_desc_rsc

   // newsgroup id (nid)
   piNews

   // don't change structure of this without changing user.kod!!!
   plMessages = $

   // plMessages is a list of lists.
   //
   // Each element list is a posted message.
   //
   // The format of each posted message is:
   //
   //   [ INT message number,
   //     OBJECT that posted the message,
   //     INT post time, (with offset, for checking deletion time)
   //     STRING unix time, (without offset, for displaying date in clients)
   //     STRING title of message,
   //     STRING message text
   //   ]

   piNext_num = 1

   piMax_messages = 75          // the # of posts we keep

messages:

   Constructor(nid = $,name = $,desc = $)
   {
      if nid = $
      {
         Debug("Newsgroup created with no nid");

         propagate;
      }

      if name <> $
      {
         vrName = name;
      }

      if desc <> $
      {
         vrDesc = desc;
      }

      piNews = nid;

      Send(SYS,@SystemNewNews,#what = self);

      propagate;
   }

   FixPostTimestamps()
   "Adds the new timestamp field to each post in plMessages."
   {
      local i;

      foreach i in plMessages
      {
         if (Length(i) < 6)
         {
            i = InsertListElem(i,NEWS_ARTICLE_LONG_TIME,
                  OldTimestampFix(Nth(i,NEWS_ARTICLE_SHORT_TIME)));
         }
      }

      return;
   }

   Delete()
   "Don't do anything on delete--stay in system's list of newsgroups, so if "
   "a room is deleted and wants to recreate us, it can find us in system.\n\n"
   "Note: this means that deleting newsgroups is currently impossible."
   {
      propagate;
   }

   UpdateTimeValues()
   {
      local i, iTime;

      iTime = GetTime();

      foreach i in plMessages
      {
         if (Nth(i,3) > iTime)
         {
            SetNth(i,3,iTime);
         }
      }

      return;
   }

   TryLook(what = $)
   {
      Send(what,@SendLookNews,#what=self);

      return;
   }

   ReqNewOwner(what = $)
   {
      return FALSE;
   }

   GetNewsNum()
   {
      return piNews;
   }

   GetNewsPermission(what = $)
   {
      return NEWS_PERMISSION_READ_WRITE; // read-write by everybody
   }

   PostNews(what = $,title = $,body = $, timestring = $)
   {
      local iMessage_num;

      if NOT (Send(self,@GetNewsPermission,#what=what) & NEWS_PERMISSION_WRITE)
      {
         Debug("ALERT!  Player ",Send(what,@GetTrueName),what,
            " tried to write to a newsglobe without permission.");

         return;
      }

      Send(self,@ClearSpaceToPost);

      iMessage_num = piNext_num++;

      title = Send(SYS,@CleanseString,#string=title);
      body = Send(SYS,@CleanseString,#string=body);

      // Passing a time in allows a post to display the original date,
      // e.g. if deleted/moved to god globe.
      if (timestring = $)
      {
         timestring = GetUnixTimeString();
      }

      SendList(Send(SYS,@GetGlobeWatchers),0,@MsgSendUser,
            #message_rsc=news_new_post,#parm1=vrName,#parm2=piNews,
            #parm3=Send(what,@GetName),#parm4=title,#parm5=iMessage_num);

      plMessages = Cons([iMessage_num,what,GetTime(),timestring,title,body],plMessages);

      return;
   }

   RemovePost(who=$, index=$)
   "Removes a post from a newsglobe, triggered by 'who'. Addresses the post "
   "by index."
   {
      local lArticle, oNews;

      // Only admins can delete from godglobe.
      if (piNews = NID_GODROOM
         AND NOT IsClass(who,&Admin))
      {
         return FALSE;
      }

      if (index = $)
      {
         Debug("RemovePost passed $ index! Not moving post.");

         return FALSE;
      }

      oNews = Send(SYS,@FindNewsByNum,#num=NID_GODROOM);
      if (oNews = $)
      {
         Debug("RemovePost couldn't find GodGlobe! Not moving post.");

         return FALSE;
      }

      // Get the article we want to move.
      lArticle = Send(self,@GetArticle,#index=index);

      if (lArticle = $)
      {
         Debug("RemovePost unable to find article with index ",index,
               " not moving post.");

         return FALSE;
      }

      if (who <> $
         AND NOT IsClass(who,&DM))
      {
         if (NOT Send(SETTINGS_OBJECT,@CanUsersRemoveOwnPosts))
         {
            Send(who,@MsgSendUser,#message_rsc=news_users_cant_delete);

            return FALSE;
         }

         if (Nth(lArticle,NEWS_ARTICLE_POSTER) <> who)
         {
            Send(who,@MsgSendUser,#message_rsc=news_not_own_post);

            return FALSE;
         }

         if (GetTime() - Nth(lArticle,NEWS_ARTICLE_SHORT_TIME)
            > viPostDeleteMinutes * 60)
         {
            // Resource to be sent to user is a time string which may change,
            // so we use a callback to add a BP_MESSAGE protocol and build
            // the packet from PostDeleteTimeExpired().
            Send(who,@MsgSendUserCallback,#what=self,
                  #sCallBack=SetString($,@PostDeleteTimeExpired));

            return FALSE;
         }
      }

      // Article is [index, player obj_id, time, timestring, title, body]
      // index and (short) time are not preserved when copying. If deleting
      // from god globe, don't try to move the post. Post will be displayed
      // on god globe with its original time (once timestring is in use),
      // meaning posts are displayed in deleted order instead of chronological.
      if (oNews <> self)
      {
         // Add a note to the title of who deleted the post.
         ClearTempString();
         AppendTempString("d:");
         AppendTempString(Send(who,@GetTrueName));
         AppendTempString(" - ");
         AppendTempString(Nth(lArticle,NEWS_ARTICLE_TITLE));

         Send(oNews,@PostNews,#what=Nth(lArticle,NEWS_ARTICLE_POSTER),
               #title=SetString($,GetTempString()),
               #body=Nth(lArticle,NEWS_ARTICLE_BODY),
               #timestring=Nth(lArticle,NEWS_ARTICLE_LONG_TIME));
      }

      Send(self,@DeleteMail,#number=index);

      return TRUE;
   }

   PostDeleteTimeExpired()
   "Called from user.kod's MsgSendUserCallback. Adds the required packets "
   "for a time string depending on how long users have to delete their own "
   "posts. Has to return TRUE so MsgSendUserCallback knows the packet was "
   "built. Don't call this directly."
   {
      Send(self,@SendTimeRemaining,#iTime=viPostDeleteMinutes * 60);

      return TRUE;
   }

   SendTimeRemainingHeader()
   "Adds the header resource for sending a time-related collection of "
   "resources to a client. Only SendTimeRemaining() should call this."
   {
      AddPacket(4, news_del_time_expired_header);

      return;
   }

   GetNumArticles()
   {
      return Length(plMessages);
   }

   GetArticleList()
   {
      return plMessages;
   }

   GetArticle(index=$)
   "Returns an article by post index."
   {
      return GetListNode(plMessages, NEWS_ARTICLE_INDEX, index);
   }

   GetNewsBody(num = $)
   {
      local i;

      foreach i in plMessages
      {
         if First(i) = num
         {
            return Nth(i,NEWS_ARTICLE_BODY);
         }
      }

      return $;
   }

   ClearSpaceToPost()
   {
      local lOld;

      // if we have too many news messages, delete oldest ones
      // to allow one message to be posted

      if Length(plMessages) < piMax_messages
      {
         return;
      }

      // unless piMax_messages has been modified, there is only
      // one node to delete.  So, be safe.

      lOld = Nth(plMessages,piMax_messages);
      while lOld <> $
      {
         plMessages = DelListElem(plMessages,lOld);
         if Length(plMessages) < piMax_messages
         {
            break;
         }

         lOld = Nth(plMessages,piMax_messages);
      }

      return;
   }

   GetNextNum()
   "Admin supported\n"
   "Prints out the next news num to help tracking down mail to delete"
   {
      return piNext_num;
   }

   DeleteMail(number = $)
   "Admin supported.\n"
   "Deletes a mail message by mail number."
   {
      local i;

      foreach i in plMessages
      {
         if First(i) = number
         {
            plMessages = DelListElem(plMessages,i);

            return TRUE;
         }
      }

      return FALSE;
   }

   DeletePost(number = $)
   "Admin supported.\n"
   "Deletes a mail message by mail number."
   {
      return Send(self,@DeleteMail,#number=number);
   }

   SearchForPosts(name = $, poster = $, contains = $)
   "Admin supported.\n"
   "Finds all posts made that fit the name, the poster, or a string it contains."
   {
      local list, i;

      if (poster = $) AND (name <> $)
      {
         poster = Send(SYS,@FindUserByString,#string=name);
      }

      if (poster = $) AND (contains = $)
      {
         return $;
      }

      foreach i in plMessages
      {
         if (poster <> $) AND (Nth(i, NEWS_ARTICLE_POSTER) = poster)
         {
            list = Cons(First(i), list);

            continue;
         }

         if (contains <> $) AND StringContain(Nth(i, NEWS_ARTICLE_TITLE), contains)
         {
            list = Cons(First(i), list);

            continue;
         }

         if (contains <> $) AND StringContain(Nth(i, NEWS_ARTICLE_BODY), contains)
         {
            list = Cons(First(i), list);

            continue;
         }
      }

      if Length(list) = 1
      {
         return First(list);
      }

      return list;
   }

   DeleteAllMessages()
   {
      plMessages = $;
      piNext_num = 1;

      return;
   }

   // Give globes a soft glow.
   SendLightingInformation()
   {
      AddPacket(2,LIGHT_FLAG_ON, 1,5, 2,LIGHT_NEWSGLOBE);

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
