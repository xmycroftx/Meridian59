// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
YrxlTree is PassiveObject

constants:

   include blakston.khd

   SAP_TIME_MIN = 60000*30  // 30 - 90 minutes
   SAP_TIME_MAX = 60000*90

resources:

   include yrxltree.lkod

   yrxltree_name_rsc = "yrxl tree"
   yrxltree_desc_rsc = \
      "The last of many yrxl trees, this is the only one to "
      "survive the thoughtless harvest of the sap of these trees "
      "for its magical properties.  It is an unspoken rule that "
      "this tree be unharmed and treated with utmost respect."

   yrxltree_icon_rsc = yrxltree.bgf

classvars:

   vrName = yrxltree_name_rsc
   vrIcon = yrxltree_icon_rsc
   vrDesc = yrxltree_desc_rsc

properties:

   ptSap = $

messages:

   Constructor()
   {
      ptSap = CreateTimer(self,@DropSap,Random(SAP_TIME_MIN,SAP_TIME_MAX));

      propagate;
   }

   CheckUserNear()
   {
      local each_obj, i;

      foreach i in Send(poOwner,@GetHolderActive)
      {
         each_obj = First(i);
         if (IsClass(each_obj,&Player)
            AND Send(each_obj,@SquareddistanceTo,#what=self) < 100)
         {
            return TRUE;
         }
      }

      return FALSE;
   }

   DropSap()
   {
      if NOT Send(self,@CheckUserNear)
         AND NOT Send(poOwner,@PossessesA,#class=&Yrxlsap)
      {
         Send(poOwner,@NewHold,#what=Create(&Yrxlsap,#Gathered=FALSE),
               #new_row=Send(self,@GetRow) + 1,
               #new_col=Send(self,@GetCol) + 1);
      }
      ptSap = CreateTimer(self,@DropSap,Random(SAP_TIME_MIN,SAP_TIME_MAX));

      return;
   }

   Delete()
   {
      if ptSap <> $
      {
         DeleteTimer(ptSap);
         ptSap = $;
      }

      propagate;
   }

   GetObjectFlags()
   {
      return OF_NOEXAMINE;
   }

end
////////////////////////////////////////////////////////////////////////////////
