// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Umbrella is RadiusEnchantment

constants:

   include blakston.khd

resources:

   include umbrella.lkod

   Umbrella_name_rsc = "umbrella"
   Umbrella_icon_rsc = iumbrela.bgf
   Umbrella_desc_rsc = \
      "Creates a dome of magical energy that protects those near the caster "
      "for several minutes."
   Umbrella_cast = \
      "You extend a mystic, shimmering dome of light out from your fingertips."
   Umbrella_starts = \
      "A mystic, shimmering dome of light emerges from %s%s's fingertips."
   Umbrella_ends = "The dome of magical energy maintained by %s%s dissipates."
   Umbrella_caster_ends = "Your dome of magical energy dissipates."
   Umbrella_caster_enter = "You feel your umbrella's magic protecting you."
   Umbrella_enter = \
      "You are encompassed by a dome of magical energy emanating from %s%s."
   Umbrella_leave = \
      "You have passed outside the dome of magical energy emanating from %s%s."
   Umbrella_deflected = "%s%s's umbrella dampens the force of your attack."

classvars:

   vrEnchantmentOn = Umbrella_cast
   vrEnchantmentOnOther = Umbrella_starts
   vrEnchantmentOff = Umbrella_caster_ends
   vrEnchantmentOffOther = Umbrella_ends
   vrEnterRadiusCaster = Umbrella_caster_enter
   vrEnterRadius = Umbrella_enter
   vrLeaveRadius = Umbrella_leave

   vrName = Umbrella_name_rsc
   vrIcon = Umbrella_icon_rsc
   vrDesc = Umbrella_desc_rsc

   viSpell_num = SID_UMBRELLA
   viMana = 15
   viManaDrain = 1400    // Mana drained per second (10^3)
   viSchool = SS_SHALILLE
   viSpell_level = 5

   viIndefinite = ARTICLE_AN
   viChance_To_Increase = 35
   viMeditate_ratio = 45
   viBaseRange = 3 * FINENESS
   viCast_Time = 2000

   viAffectsGuildmates = TRUE
   viAffectsEnemies = TRUE

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Emerald,2],plReagents);

      return;
   }

   EndEnchantment(who=$)
   {
      if IsClass(who,&User)
      {
         Post(who,@DrawResistances);
      }

      propagate;
   }

   ModifyDefensePower(who=$,what=$,defense_power=0,state=$)
   {
      if (state = $)
      {
         return defense_power;
      }

      if what <> $
      {
         if Send(what,@IsAffectedByRadiusEnchantment,#what=self,#state=state)
         {
            // Umbrella can't help if the attacker is within its limits
            return defense_power;
         }
         Post(what,@MsgSendUser,#message_rsc=Umbrella_deflected,
               #parm1=Send(who,@GetCapDef),#parm2=Send(who,@GetName));
      }

      // Adds up to 198 defense against attacks from outside the umbrella
      return defense_power + Nth(state,5) * 2;
   }

   ModifyResistance(resistance_list=$,iState=0)
   {
      local iResist;

      // 0-10% resist all from any attack to make the spell worth using.
      iResist = iState / 5;

      resistance_list = Send(SYS,@AddResistance,#what=-SPL_TYPE_ALL,
                              #value=iResist,#resistance_list=resistance_list);
      resistance_list = Send(SYS,@AddResistance,#what=ATK_TYPE_ALL,
                              #value=iResist,#resistance_list=resistance_list);

      return resistance_list;
   }

end
////////////////////////////////////////////////////////////////////////////////
