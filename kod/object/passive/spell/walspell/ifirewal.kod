// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
IllusionaryFireWall is WallSpell

constants:

   include blakston.khd

resources:

   include ifirewal.lkod

   IllusionaryFireWall_name_rsc = "illusionary firewall"
   IllusionaryFireWall_icon_rsc = iwallill.bgf
   IllusionaryFireWall_desc_rsc = \
      "Creates an illusionary wall of fire.  This wall will cause illusionary "
      "wounds, which are never lethal, to victims walking through it."

   IllusionaryFireWall_cast_rsc = \
      "An illusionary wall of fire appears before you!"

   IllusionaryFireWall_sound = farenwala822m.ogg

classvars:

   vrName = IllusionaryFireWall_name_rsc
   vrIcon = IllusionaryFireWall_icon_rsc
   vrDesc = IllusionaryFireWall_desc_rsc

   viSpell_num = SID_ILLUSIONARY_FIREWALL
   viSchool = SS_RIIJA
   viSpell_level = 3
   viMana = 15
   viSpellExertion = 3
   viMeditate_ratio = 30

   viCast_time = 1000
   vrSucceed_wav = IllusionaryFireWall_sound
   vrSuccess = IllusionaryFireWall_cast_rsc

   viHarmful = TRUE

   // Illusionary fire can't kill anyone, so don't warn about being a murderer.
   viWallCanKill = FALSE

   //// Seed parameters
   viElementStep = 40

properties:


messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&RainbowFern,3],plReagents);
      plReagents = Cons([&Firesand,2], plReagents);

      return;
   }

   PlaceWallElements(seed = $)
   {
      local lSeed, lSeeda, lSeedb, lSecondarySeeda, lSecondarySeedb,
         oRoom, oCaster, iXFineStep, iYFineStep, iXFineStepA, iYFineStepA,
         iXFineStepB, iYFineStepB, iSpellPower, iDuration, iCharges, iSpeed,
         iAngle, iStep, iNoise, iCurl;

      oCaster = First(seed);
      oRoom = Send(oCaster,@GetOwner);
      iSpellPower = Nth(seed,2);
      iDuration = Nth(seed,3);
      iCharges = Nth(seed,4);
      iSpeed = Nth(seed,5);
      iAngle = Nth(seed,6);
      iStep = Nth(seed,7);
      iNoise = Nth(seed,8);
      iCurl = Nth(seed,9);

      iXFineStep = Send(oRoom,@GetXFineStep,#angle=iAngle);
      iYFineStep = Send(oRoom,@GetYFineStep,#angle=iAngle);

      lSecondarySeeda = [oCaster,iSpellPower,iDuration,iCharges,
         iSpeed,3*MAX_ANGLE/4,iStep,iNoise,-iCurl];
      lSecondarySeedb = [oCaster,iSpellPower,iDuration,iCharges,
         iSpeed,MAX_ANGLE/4,iStep,iNoise,iCurl];

      lSeed = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,0];
      lSeeda = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,iCurl,lSecondarySeeda];
      lSeedb = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,-iCurl,lSecondarySeedb];

      Send(self,@SpawnElement,#seed=lSeed,#who=oCaster,
         #xfineoffset=iXFineStep,#yfineoffset=iYFineStep,
         #walltype=&IllusionaryWallOfFire);

      iXFineStepA = iXFineStep + Send(oRoom,@GetXFineStep,
         #angle=(iAngle+3*MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      iYFineStepA = iYFineStep + Send(oRoom,@GetYFineStep,
         #angle=(iAngle+3*MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      Send(self,@SpawnElement,#seed=lSeeda,#who=oCaster,
         #xfineoffset=iXFineStepA,#yfineoffset=iYFineStepA,
         #walltype=&IllusionaryWallOfFire);

      iXFineStepB = iXFineStep + Send(oRoom,@GetXFineStep,
         #angle=(iAngle+MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      iYFineStepB = iYFineStep + Send(oRoom,@GetYFineStep,
         #angle=(iAngle+MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      Send(self,@SpawnElement,#seed=lSeedb,#who=oCaster,
         #xfineoffset=iXFineStepB,#yfineoffset=iYFineStepB,
         #walltype=&IllusionaryWallOfFire);

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
