// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
LightningWall is WallSpell

constants:

   include blakston.khd

resources:

   include ltngwall.lkod

   LightningWall_name_rsc = "lightning wall"
   LightningWall_icon_rsc = iwalllit.bgf
   LightningWall_desc_rsc = \
      "Creates a crackling barrier of electricity.  One who entered such a "
      "field would certainly feel more than thunder."

   LightningWall_cast_rsc = "A wall of lightning appears before you!"

   LightningWall_sound = faren.ogg

classvars:

   vrName = LightningWall_name_rsc
   vrIcon = LightningWall_icon_rsc
   vrDesc = LightningWall_desc_rsc

   viSpell_num = SID_LIGHTNING_WALL
   viSchool = SS_FAREN
   viSpell_level = 6
   viMana = 20
   viSpellExertion = 3

   viChance_To_Increase = 25
   viMeditate_ratio = 75
   viCast_time = 1000
   vrSucceed_wav = LightningWall_sound
   vrSuccess = LightningWall_cast_rsc

   viHarmful = TRUE

   //// Seed parameters
   viElementStep = 40

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&BlueMushroom,3],plReagents);
      plReagents = Cons([&Yrxlsap,1], plReagents);

      return;
   }

   PlaceWallElements(seed = $)
   {
      local lSeed, lSeeda, lSeedb, lSecondarySeeda, lSecondarySeedb,
         oRoom, oCaster, iXFineStep, iYFineStep, iXFineStepA, iYFineStepA,
         iXFineStepB, iYFineStepB, iSpellPower, iDuration, iCharges, iSpeed,
         iAngle, iStep, iNoise, iCurl;

      oCaster = First(seed);
      oRoom = Send(oCaster,@GetOwner);
      iSpellPower = Nth(seed,2);
      iDuration = Nth(seed,3);
      iCharges = Nth(seed,4);
      iSpeed = Nth(seed,5);
      iAngle = Nth(seed,6);
      iStep = Nth(seed,7);
      iNoise = Nth(seed,8);
      iCurl = Nth(seed,9);

      iXFineStep = Send(oRoom,@GetXFineStep,#angle=iAngle);
      iYFineStep = Send(oRoom,@GetYFineStep,#angle=iAngle);

      lSecondarySeeda = [oCaster,iSpellPower,iDuration,iCharges,
         iSpeed,3*MAX_ANGLE/4,iStep,iNoise,-iCurl];
      lSecondarySeedb = [oCaster,iSpellPower,iDuration,iCharges,
         iSpeed,MAX_ANGLE/4,iStep,iNoise,iCurl];

      lSeed = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,0];
      lSeeda = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,iCurl,lSecondarySeeda];
      lSeedb = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,-iCurl,lSecondarySeedb];

      Send(self,@SpawnElement,#seed=lSeed,#who=oCaster,
         #xfineoffset=iXFineStep,#yfineoffset=iYFineStep,
         #walltype=&WallOfLightning);

      iXFineStepA = iXFineStep + Send(oRoom,@GetXFineStep,
         #angle=(iAngle+3*MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      iYFineStepA = iYFineStep + Send(oRoom,@GetYFineStep,
         #angle=(iAngle+3*MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      Send(self,@SpawnElement,#seed=lSeeda,#who=oCaster,
         #xfineoffset=iXFineStepA,#yfineoffset=iYFineStepA,
         #walltype=&WallOfLightning);

      iXFineStepB = iXFineStep + Send(oRoom,@GetXFineStep,
         #angle=(iAngle+MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      iYFineStepB = iYFineStep + Send(oRoom,@GetYFineStep,
         #angle=(iAngle+MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      Send(self,@SpawnElement,#seed=lSeedb,#who=oCaster,
         #xfineoffset=iXFineStepB,#yfineoffset=iYFineStepB,
         #walltype=&WallOfLightning);

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
