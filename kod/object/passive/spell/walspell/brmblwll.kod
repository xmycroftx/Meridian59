// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
BrambleWall is WallSpell 

constants:

   include blakston.khd

resources:

   include brmblwll.lkod

   BrambleWall_name_rsc = "bramble wall"
   BrambleWall_icon_rsc = iwalltho.bgf
   BrambleWall_desc_rsc = \
      "Creates a thicket of thorny, twisted bramble.  Such a wall would be "
      "painful to pass, though it might not harm one who remained "
      "motionless."

   BrambleWall_cast_rsc = "A wall of brambles appears before you!"

   BrambleWall_sound = faren.ogg

classvars:

   vrName = BrambleWall_name_rsc
   vrIcon = BrambleWall_icon_rsc
   vrDesc = BrambleWall_desc_rsc

   viSpell_num = SID_BRAMBLE_WALL
   viSchool = SS_FAREN
   viSpell_level = 2
   viMana = 10

   viChance_To_Increase = 25
   viMeditate_ratio = 20
   viSpellExertion = 2
   viCast_time = 4000
   vrSucceed_wav = BrambleWall_sound
   vrSuccess = BrambleWall_cast_rsc

   viHarmful = TRUE

   //// Seed parameters
   viElementStep = 40

properties:


messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Elderberry,3],plReagents);
      plReagents = Cons([&Yrxlsap,1], plReagents);

      return;
   }

   OfferToNewCharacters()
   {
      // Wall spells are difficult to use.
      return FALSE;
   }

   PlaceWallElements(seed = $)
   {
      local lSeed, lSeeda, lSeedb, lSecondarySeeda, lSecondarySeedb,
         oRoom, oCaster, iXFineStep, iYFineStep, iXFineStepA, iYFineStepA,
         iXFineStepB, iYFineStepB, iSpellPower, iDuration, iCharges, iSpeed,
         iAngle, iStep, iNoise, iCurl;

      oCaster = First(seed);
      oRoom = Send(oCaster,@GetOwner);
      iSpellPower = Nth(seed,2);
      iDuration = Nth(seed,3);
      iCharges = Nth(seed,4);
      iSpeed = Nth(seed,5);
      iAngle = Nth(seed,6);
      iStep = Nth(seed,7);
      iNoise = Nth(seed,8);
      iCurl = Nth(seed,9);

      iXFineStep = Send(oRoom,@GetXFineStep,#angle=iAngle);
      iYFineStep = Send(oRoom,@GetYFineStep,#angle=iAngle);

      lSecondarySeeda = [oCaster,iSpellPower,iDuration,iCharges,
         iSpeed,3*MAX_ANGLE/4,iStep,iNoise,-iCurl];
      lSecondarySeedb = [oCaster,iSpellPower,iDuration,iCharges,
         iSpeed,MAX_ANGLE/4,iStep,iNoise,iCurl];

      lSeed = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,0];
      lSeeda = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,iCurl,lSecondarySeeda];
      lSeedb = [oCaster,iSpellPower,iDuration,iCharges/2+1,
         iSpeed,iAngle,iStep,iNoise,-iCurl,lSecondarySeedb];

      Send(self,@SpawnElement,#seed=lSeed,#who=oCaster,
         #xfineoffset=iXFineStep,#yfineoffset=iYFineStep,
         #walltype=&Brambles);

      iXFineStepA = iXFineStep + Send(oRoom,@GetXFineStep,
         #angle=(iAngle+3*MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      iYFineStepA = iYFineStep + Send(oRoom,@GetYFineStep,
         #angle=(iAngle+3*MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      Send(self,@SpawnElement,#seed=lSeeda,#who=oCaster,
         #xfineoffset=iXFineStepA,#yfineoffset=iYFineStepA,
         #walltype=&Brambles);

      iXFineStepB = iXFineStep + Send(oRoom,@GetXFineStep,
         #angle=(iAngle+MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      iYFineStepB = iYFineStep + Send(oRoom,@GetYFineStep,
         #angle=(iAngle+MAX_ANGLE/4) % MAX_ANGLE)*iStep/FINENESS;
      Send(self,@SpawnElement,#seed=lSeedb,#who=oCaster,
         #xfineoffset=iXFineStepB,#yfineoffset=iYFineStepB,
         #walltype=&Brambles);

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
