// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
SporeBurst is WallSpell

constants:

   include blakston.khd

resources:

   include sporbrst.lkod

   SporeBurst_name_rsc = "spore burst"
   SporeBurst_icon_rsc = isporecl.bgf
   SporeBurst_desc_rsc = \
      "Summons forth noxious clouds that paralyze any within it's bounds."
   SporeBurst_cast_rsc = \
      "Following a loud popping sound, the room is filled with a cloud of "
      "choking spore mold."
   SporeBurst_sound = farenspore822m.ogg

classvars:

   vrName = SporeBurst_name_rsc
   vrIcon = SporeBurst_icon_rsc
   vrDesc = SporeBurst_desc_rsc

   viSpell_num = SID_SPORE_BURST
   viSchool = SS_FAREN
   viSpell_level = 6
   viMana = 20
   viSpellExertion = 5
   viMeditate_ratio = 75

   viChance_To_Increase = 15

   vrSucceed_wav = SporeBurst_sound
   vrSuccess = SporeBurst_cast_rsc

   viHarmful = TRUE
   vbCanCastOnOthers = FALSE

   // Sporeburst can't kill anyone, so don't warn about being a murderer.
   viWallCanKill = FALSE

   viCooldownSID = SID_COOLDOWN_SPOREBURST

   //// Seed parameters
   viElementDuration = 10000
   viElementSpeed = 50
   viElementCharges = 27
   viElementNoise = 0
   viElementCurve = 961024

properties:

   piBaseDuration = 5000
   piSpellPowerModifier = 50

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&PurpleMushroom,2],plReagents);
      plReagents = Cons([&ShamanBlood,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return vbCanCastOnOthers;
   }

   PlaceWallElements(seed = $, lTargets = $)
   {
      local lSeed;

      lSeed = ListCopy(seed);
      SetNth(lSeed,6,0);

      Send(self,@SpawnElement,#seed=lSeed,#who=First(seed),
         #yfineoffset=FINENESS_HALF,#xfineoffset=FINENESS_HALF,
         #walltype=&ActiveSporeCloud);

      propagate;
   }

   ModifyMonsterBehavior(mob = $)
   {
      Send(mob,@SetBehaviorFlag,#flag=AI_NOMOVE,#value=TRUE);
      Send(mob,@SetBehaviorFlag,#flag=AI_NOFIGHT,#value=TRUE);

      return;
   }

   GetScrollClass()
   {
      return &SporeBurstScroll;
   }

end
////////////////////////////////////////////////////////////////////////////////
