// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Pardon is DMSpell

constants:

   include blakston.khd

resources:

   include pardon.lkod

   pardon_name_rsc = "pardon"
   pardon_icon_rsc = light.bgf
   pardon_desc_rsc = "Removes both murderer flags and outlaw flags."

   pardon_msg_DM = "You pardon %s%s."
   pardon_msg_target = "You have been pardoned by %s%s."
   pardon_failed = \
      "%s%s is not a murderer or an outlaw, and cannot be pardoned."

classvars:

   vrName = pardon_name_rsc
   vrIcon = pardon_icon_rsc
   vrDesc = pardon_desc_rsc

   viSpell_num = SID_PARDON
   viSchool = SS_DM_COMMAND

properties:

messages:

   GetNumSpellTargets()
   {
      return 1;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local oTarget;

      oTarget = First(lTargets);

      if NOT IsClass(oTarget,&User)
      {
         Send(who,@MsgSendUser,#message_rsc=pardon_failed,
               #parm1=Send(oTarget,@GetDef),#parm2=Send(oTarget,@GetName));

         return FALSE;
      }

      propagate;
   }

   DoSpellEffects(who = $, lTargets = $)
   {
      local i;

      foreach i in lTargets
      {
         if Send(i,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
            OR Send(i,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
         {
            Send(who,@MsgSendUser,#message_rsc=pardon_msg_DM,
                  #parm1=Send(i,@GetDef),#parm2=Send(i,@GetName));
            Send(i,@MsgSendUser,#message_rsc=pardon_msg_target,
                  #parm1=Send(who,@GetIndef),#parm2=Send(who,@GetName));

            Send(i,@SetPlayerFlag,#flag=PFLAG_MURDERER,#value=FALSE);
            Send(i,@SetPlayerFlag,#flag=PFLAG_OUTLAW,#value=FALSE);
            // Reset phase/ghost time.
            Send(i,@RefreshPhaseTimeToBase);
            Post(i,@EvaluatePKStatus);
         }
         else
         {
            Send(who,@MsgSendUser,#message_rsc=pardon_failed,
                  #parm1=Send(i,@GetDef),#parm2=Send(i,@GetName));

            return FALSE;
         }
      }

      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
