// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Villify is DMSpell

// (sic-- it's supposed to be 'vilify'.)

constants:

   include blakston.khd

resources:

   include villify.lkod

   villify_name_rsc = "vilify"
   villify_icon_rsc = light.bgf
   villify_desc_rsc = "Accuses someone of being an outlaw or murderer."

   villify_msg_DM = "You vilify %s."
   villify_msg_target = "You have been vilified by %s."
   villify_failed = "%s%s is already a murderer."

classvars:

   vrName = villify_name_rsc
   vrIcon = villify_icon_rsc
   vrDesc = villify_desc_rsc

   viSpell_num = SID_VILLIFY
   viSchool = SS_DM_COMMAND

properties:

messages:

   GetNumSpellTargets()
   {
      return 1;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      if NOT IsClass(First(lTargets),&User)
      {
         Send(who,@MsgSendUser,#message_rsc=villify_failed,
               #parm1=Send(First(lTargets),@GetDef),
               #parm2=Send(First(lTargets),@GetTrueName));

         return FALSE;
      }

      propagate;
   }

   DoSpellEffects(who = $, lTargets = $)
   {
      local i;

      foreach i in lTargets
      {
         // already a murderer
         if Send(i,@CheckPlayerFlag,#flag=PFLAG_MURDERER)
         {
            Send(who,@MsgSendUser,#message_rsc=villify_failed,
                  #parm1=Send(i,@GetDef),#parm2=Send(i,@GetTrueName));

            continue;
         }

         // from outlaw to murderer
         if Send(i,@CheckPlayerFlag,#flag=PFLAG_OUTLAW)
         {
            Send(who,@MsgSendUser,#message_rsc=villify_msg_DM,
                  #parm1=Send(i,@GetTrueName));
            Send(i,@MsgSendUser,#message_rsc=villify_msg_target,
                  #parm1=Send(who,@GetTrueName));
            Send(i,@MakePlayerMurderer);

            continue;
         }

         // from lawful to outlaw
         Send(who,@MsgSendUser,#message_rsc=villify_msg_DM,
               #parm1=Send(i,@GetTrueName));
         Send(i,@MsgSendUser,#message_rsc=villify_msg_target,
               #parm1=Send(who,@GetTrueName));
         Send(i,@SetPlayerFlag,#flag=PFLAG_OUTLAW,#value=TRUE);
         Send(i,@SetPlayerFlag,#flag=PFLAG_MURDERER,#value=FALSE);
         // Reset phase/ghost time.
         Send(who,@RefreshPhaseTimeToBase);

         Post(i,@EvaluatePKStatus);
      }

      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
