// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
// The only way to lose this enchantment is to break the trance.
ManaFocus is PersonalEnchantment

constants:

   include blakston.khd

resources:

   include focus.lkod

   manafocus_name_rsc = "mana focus"
   manafocus_icon_rsc = imanafoc.bgf
   manafocus_desc_rsc = \
      "Focuses your magical energy, regenerating your mana at an accelerated "
      "rate and increasing the damage of natural elemental attack spells. "
      "Dissipates if your concentration is broken."
   manafocus_enchantment_rsc = \
      "\n\nYour current %s enchantment was cast with %i spellpower."
   manafocus_on = "You feel newly focused magic raging through your body."
   manafocus_break_trance = "You have lost your mana focus."
   manafocus_already_enchanted = "You already have %s%s."

classvars:

   vrName = manafocus_name_rsc
   vrIcon = manafocus_icon_rsc
   vrDesc = Manafocus_desc_rsc

   vrAlreadyEnchanted = manafocus_already_enchanted
   vrEnchantment_On = manafocus_on
   vrEnchantment_Off = manafocus_break_trance
   vrSuccess = manafocus_on

   viSpell_num = SID_MANA_FOCUS
   viMana = 1
   viSchool = SS_FAREN
   viSpell_level = 3

   viCast_time = 3000            /// 3 seconds
   viChance_To_Increase = 15
   viMeditate_ratio = 20

   vbCanCastOnOthers = FALSE

properties:

   // The effectiveness of purge on the target spell as a percentage.
   // 100 will cause purge to remove its entire spellpower, 0 will
   // prevent purge from removing this spell.
   viPurgeFactor = 5

   piMovementSpeedReduction = 30

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Mushroom,1],plReagents);

      return;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetTranceFlag);
      Send(who,@SetPlayerFlag,#flag=PFLAG_MANA_FOCUS,#value=TRUE);

      return;
   }

   AdjustManaTime(time=0, iSpellPower=0)
   "Reduces time passed in so that mana regens faster.  Returns reduced time."
   {
      local iTime;

      iTime = (time * (200-iSpellPower)) / 200;
      iTime = Bound(iTime,500,60000);

      return iTime;
   }

   GetStateValue(iSpellpower=0,target=$)
   {
      Send(target,@AddMovementSpeedPercent,#iSpeed=-piMovementSpeedReduction);

      return iSpellPower;
   }

   EndEnchantment(who=$,report=TRUE,state=0)
   {
      Send(who,@AddMovementSpeedPercent,#iSpeed=piMovementSpeedReduction);

      propagate;
   }

   GetDuration(iSpellPower=0)
   {
      return SPELL_INDEFINITE_DURATION;
   }

   EffectDesc(who=$)
   {
      AddPacket(4,manafocus_enchantment_rsc, 4,Send(self,@GetName),
                4,Send(who,@GetEnchantedState,#what=self));

      return;
   }

   CanPayCosts(who=$,lTargets=$,bItemCast=FALSE)
   {
      if Send(who,@IsEnchanted,#what=self)
      {
         Send(self,@BreakTrance,#who=who,#event=EVENT_CANCEL,
            #state=Send(who,@GetEnchantedState,#what=self));

         return FALSE;
      }

      propagate;
   }

   BreakTrance(event=$,what=$,state=$)
   {
      if event = EVENT_CAST
         AND ((what = self) OR IsClass(what,&AttackSpell))
      {
         return FALSE;
      }

      propagate;
   }

   TranceEnding(who=$,state=$)
   "Allow maintained spells to do something when their trance breaks."
   {
      Send(who,@RemoveEnchantment,#what=self,#state=state);

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////
