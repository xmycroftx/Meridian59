// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
CreateSpiritualHammer is Spell

constants:

   include blakston.khd

resources:

   include creasphm.lkod

   createspiritualhammer_name_rsc = "spiritual hammer"
   createspiritualhammer_icon_rsc = ispirith.bgf
   createspiritualhammer_desc_rsc = \
      "Materializes a spiritual hammer."
   createspiritualhammer_cast_rsc = "A spiritual hammer appears."
   createspiritualhammer_inv_full_rsc = \
      "A spiritual appears in the air before you, but before you "
      "can drop something and grab it, it vanishes."
   createspiritualhammer_sound = sspham.ogg

classvars:

   vrName = createspiritualhammer_name_rsc
   vrIcon = createspiritualhammer_icon_rsc
   vrDesc = createspiritualhammer_desc_rsc
   
   viSpell_num = SID_SPIRITUAL_HAMMER
   viSchool = SS_SHALILLE
   viSpell_level = 3
   viMana = 15
   viSpellExertion = 2
   viCast_time = 5000
   viMeditate_ratio = 30

   vrSucceed_wav = createspiritualhammer_sound

properties:

messages:

   ResetReagents()
   {
      // separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&Emerald,2],plReagents);
      plReagents = Cons([&OrcTooth,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   GetDuration(iSpellPower = 0)
   {
      local iDuration;

      iDuration = 10 + ((50 * iSpellpower) / 100);  // lasts 10 - 60 minutes

      return iDuration * 60 * 1000;  // Convert to ms
   }

   DoSpellEffects(who = $, iDuration = $, iSpellPower = $)
   {
      local oSpiritualHammer, iToHitBonus;

      if (iDuration = $)
      {
         iDuration = Send(self,@GetDuration,#iSpellPower=iSpellPower);
      }

      iToHitBonus = (iSpellPower * 3) / 2;
      oSpiritualHammer = Create(&SpiritualHammer,#iLongevity=iDuration,
                              #iToHitBonus=iToHitBonus);

      if Send(who,@ReqNewHold,#what=oSpiritualHammer)
      {
         Send(who,@NewHold,#what=oSpiritualHammer);
         Send(who,@MsgSendUser,#message_rsc=createspiritualhammer_cast_rsc);
      }
      else
      {
         Send(oSpiritualHammer,@Delete);
         Send(who,@MsgSendUser,#message_rsc=createspiritualhammer_inv_full_rsc);
      }

      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
